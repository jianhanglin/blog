---
layout: post
title: X64操作系统开发
category: osdev
---
> 多数操作系统开发教程都是基于IA-32架构，但是我一直想尝试64位架构，这个项目就是我的一个尝试，本文相当于这个Hobbyist OS的开发笔记。

### 开发测试环境

根据前人经验，我选择在Linux系统下，使用C语言和汇编语言进行开发，其中C语言为主，汇编语言只有在没有其他选择的情况下才会涉及。然而，与多数Linux用户使用ATT汇编语法的习惯不同，我使用Intel语法，汇编器使用Yasm，这是Nasm的增强版本，尤其适合编写64位代码。C语言编译器使用Clang，理论上与GCC兼容，但是错误报告更加友好，而且据说优化效果胜过GCC。

内核编译为纯二进制格式，使用GRUB（Legacy）引导，这样就避免了手动编写引导扇区的麻烦。系统测试使用QEMU虚拟机，引导软盘镜像启动。

自动化构建工具GNUmake用来编译整个项目，Makefile是手动编写而非自动生成的。Makefile自动发现所有的源代码文件，根据后缀名不同分为不同类型执行不同编译操作，最终链接成统一的`kernel.bin`。

~~~ {.makefile}
# directories
SRCDIRS :=  x64 kernel library
INCDIRS :=  include
BLDDIR  :=  build

# search files using suffix
SOURCES :=  $(foreach DIR, $(SRCDIRS), $(wildcard $(DIR)/*.asm $(DIR)/*.c))
HEADERS :=  $(foreach DIR, $(INCDIRS), $(wildcard $(DIR)/*.h))
OBJECTS :=  $(foreach OBJECT, $(patsubst %.asm, %.asm.o, \
                $(patsubst %.c, %.c.o, $(SOURCES))), $(BLDDIR)/$(OBJECT))
TARGET  :=  $(BLDDIR)/kernel.bin
FLOPPY  :=  fd.img

# toolchain
AS	:=	yasm
AFLAGS	:=	-f elf64
CC	:=	clang
CFLAGS	:=	-c -std=c99 -I $(INCDIRS) \
		-ffreestanding -fno-builtin -nostdlib -nostdinc \
		-mcmodel=large -mno-mmx -mno-sse -mno-sse2 -mno-sse3 -mno-3dnow \
		-Wall -Wextra
LD	:=	ld
LFLAGS	:=	-T link.lds

# pseudo-targets
.PHONY	:=	bin write run all clean

bin:	$(TARGET)

# This command needs to be run as root.
write:	$(TARGET) $(FLOPPY)
	mount $(FLOPPY) /mnt
	cp $(TARGET) /mnt
	umount /mnt

run:	$(FLOPPY)
	qemu-system-x86_64 -m 32 -smp 2 -fda $(FLOPPY)

all:    build write run

clean:
	- rm $(OBJECTS) $(TARGET)

$(TARGET):	$(OBJECTS) link.lds
	mkdir -p $(@D)
	$(LD) $(LFLAGS) -o $@ $^

$(BLDDIR)/%.asm.o:	%.asm
	mkdir -p $(@D)
	$(AS) $(AFLAGS) -o $@ $<

$(BLDDIR)/%.c.o:	%.c $(HEADERS)
	mkdir -p $(@D)
	$(CC) $(CFLAGS) -o $@ $<
~~~

### 一个可以开机的“OS”

开发的第一步，就是做一个能够引导的内核。前文说到，我使用GRUB作为引导器，要让GRUB能够引导自己的操作系统内核，那么内核镜像就必须满足GRUB的要求。GRUB遵循的是Multiboot Specification，根据此规范，内核镜像必须包含Multiboot Header结构。

Multiboot Header中需要内核的一些信息，如内核加载到的内存地址，内核在内存中的结束位置，BSS段的结束位置，相当于编写这个内核的同时就要知道内核编译之后的大小，解决方法就是让链接器重定位的时候完成。

Linux工具链中的链接器ld有一个非常有用的功能，那就是可以通过脚本控制链接器的行为，下面就是该系统使用的ld链接脚本：

~~~ {.ld}
OUTPUT_FORMAT(binary)

SECTIONS {
    . = 1M;
    kernel_load_addr = .;
    .text : ALIGN(4) {
        seg_text_start_addr = .;
        *(.bootstrap)
        *(.text)
        seg_text_end_addr = .;
    } = 0x90
    .data : ALIGN(4) {
        seg_data_start_addr = .;
        *(.rodata)
        *(.data)
        seg_data_end_addr = .;
    } = 0x90
    .bss : ALIGN(4) {
        seg_bss_start_addr = .;
        *(COMMON)
        *(.bss)
        seg_bss_end_addr = .;
    } = 0x90
}
~~~

这个脚本首先指定输出格式为纯二进制格式，然后设置起始地址为1M，也就是内核将会加载到物理内存地址1M位置处（因为1M以下的内存千疮百孔，最好别动）。另外还定义了若干section，将每个section的起始地址、结束地址记录在了变量中，这些变量（确切地说应该是符号）在C语言、汇编语言的程序中是可以引用的，这样就能知道内核在内存中的布局了。

Multiboot Header在汇编程序boot.asm中提供，代码如下：

~~~ {.assembly}
extern kernel_load_addr
extern seg_data_end_addr
extern seg_bss_end_addr

MB1_MAGIC equ 0x1badb002
MB1_FLAGS equ 1<<0|1<<1|1<<16
MB1_CHECK equ -(MB1_MAGIC+MB1_FLAGS)

[section .bootstrap]
[BITS 32]
        jmp     multiboot_entry

ALIGN 4
multiboot_header:
        dd      MB1_MAGIC
        dd      MB1_FLAGS
        dd      MB1_CHECK

        dd      multiboot_header        ; header addr
        dd      kernel_load_addr        ; load addr
        dd      seg_data_end_addr       ; load end addr
        dd      seg_bss_end_addr        ; bss end addr
        dd      multiboot_entry         ; entry addr

multiboot_entry:
        ;; put code here
~~~

内核被GRUB引导起来，就会从`multiboot_entry`的地方开始执行，可以写一段程序，向屏幕打印一个字母：

~~~ {.asembly}
multiboot_entry:
        mov     ah, 0x0E    ; 黑底黄色字
        mov     al, 'H'
        mov     [0xb8000], ax
        jmp     $           ; 死循环
~~~

### 进入64位环境

当机器刚刚启动时，CPU处于实模式，GRUB将模式切换到保护模式并将控制权交给内核。也就是说，当boot.asm的代码运行起来时，CPU处于保护模式。从32位保护模式向64位长方式的转换部分主要参考了osdev上的[这篇文章](http://wiki.osdev.org/User:Stephanvanschaik/Setting_Up_Long_Mode)，代码如下：

~~~ {.assembly}
extern init

extern kernel_load_addr
extern seg_data_end_addr
extern seg_bss_end_addr

MB1_MAGIC equ 0x1badb002
MB1_FLAGS equ 1<<0|1<<1|1<<16
MB1_CHECK equ -(MB1_MAGIC+MB1_FLAGS)

[section .bootstrap]
[BITS 32]
        jmp     multiboot_entry

ALIGN 4
multiboot_header:
        dd      MB1_MAGIC
        dd      MB1_FLAGS
        dd      MB1_CHECK

        dd      multiboot_header        ; header addr
        dd      kernel_load_addr        ; load addr
        dd      seg_data_end_addr       ; load end addr
        dd      seg_bss_end_addr        ; bss end addr
        dd      multiboot_entry         ; entry addr

multiboot_entry:
        ; disable interruption
        cli

        ; store boot information
        mov     [multiboot_magic], eax
        mov     [multiboot_info], ebx
        
        ; setup kernel stack
        mov     esp, kernel_stack_top

        ; clear flags register
        push    0
        popf

        mov     ebp, 0

check_long_mode:
        ; check CPUID
        pushfd
        pop     eax
        mov     ecx, eax
        xor     eax, 1 << 21
        push    eax
        popfd
        pushfd
        pop     eax
        push    ecx
        popfd
        xor     eax, ecx
        jz      no_long_mode
        
        ; check CPUID extended functions
        mov     eax, 0x80000000
        cpuid
        cmp     eax, 0x80000001
        jb      no_long_mode
        
        ; check long mode  existence
        mov     eax, 0x80000001
        cpuid
        test    edx, 1 << 29
        jz      no_long_mode

enter_long_mode:
        ; disable paging (to be safe)
        mov     eax, cr0
        and     eax, 0x7fffffff
        mov     cr0, eax

        ; clear the memory from 4K to 20K.
        mov     edi, 0x1000
        mov     cr3, edi
        xor     eax, eax
        mov     ecx, 4096
        rep     stosd
        mov     edi, cr3

        ; setup PLM4, PLT, PDT
        mov     dword [edi], 0x2003
        add     edi, 0x1000
        mov     dword [edi], 0x3003
        add     edi, 0x1000
        mov     dword [edi], 0x4003
        add     edi, 0x1000

        mov     ebx, 0x00000003
        mov     ecx, 512
.set_entry:
        mov     dword [edi], ebx
        add     ebx, 0x1000
        add     edi, 8
        loop    .set_entry

        ; enable PAE-paging
        mov     eax, cr4
        or      eax, 1 << 5
        mov     cr4, eax

        ; set LM-bit
        mov     ecx, 0xc0000080
        rdmsr
        or      eax, 1 << 8
        wrmsr

        ; enable paging
        mov     eax, cr0
        or      eax, 1 << 31
        mov     cr0, eax

        ; enter 64-bit submode
        lgdt    [gdt.ptr]
        jmp     gdt.code0:long_mode_entry

        ; should never return here
        jmp     $

no_long_mode:
        ; clear screen
        mov     edi, 0xb8000
        mov     eax, 0x0f200f20
        mov     ecx, 1000
        rep     stosd

        ; write error message
        mov     edi, 0xb8000
        mov     esi, err_msg
        mov     ecx, err_msg_len
        mov     ah, 0x0f
.print:
        lodsb
        stosw
        loop    .print

        hlt
        jmp     $

[BITS 64]
long_mode_entry:
        cli
        mov     ax, gdt.data0
        mov     ds, ax
        mov     es, ax
        mov     edi, 0xb8000            ; Clear screen
        mov     rax, 0x1f201f201f201f20
        mov     ecx, 500
        rep     stosq

        mov     edi, [multiboot_magic]  ; zero extend to rdi
        mov     esi, [multiboot_info]   ; zero extend to rsi
        call    init

        ; halt on return
        hlt
        jmp     $

[section .data]
[BITS 32]
gdt:            ; Global Descriptor Table (64-bit).
.null:  equ     $ - gdt     ; null descriptor
        dq      0
.code0: equ     $ - gdt     ; code descriptor
        dw      0           ; limit [0:15]
        dw      0           ; base [0:15]
        db      0           ; base [16:23]
        db      10011000b   ; Present, DPL=0, non-conforming
        db      00100000b   ; 64-bit
        db      0           ; base [24:31]
.data0: equ     $ - gdt     ; data descriptor
        dw      0           ; limit [0:15]
        dw      0           ; base [0:15]
        db      0           ; base [16:23]
        db      10010000b   ; Present, DPL=0
        db      0           ; limit [16:19] and attr
        db      0           ; base [24:31]
.code3: equ     $ - gdt     ; code descriptor
        dw      0           ; limit [0:15]
        dw      0           ; base [0:15]
        db      0           ; base [16:23]
        db      11111000b   ; Present, DPL=0, non-conforming
        db      00100000b   ; 64-bit
        db      0           ; base [24:31]
.data3: equ     $ - gdt     ; data descriptor
        dw      0           ; limit [0:15]
        dw      0           ; base [0:15]
        db      0           ; base [16:23]
        db      11110000b   ; Present, DPL=0
        db      0           ; limit [16:19] and attr
        db      0           ; base [24:31]

.ptr:           ; The GDT-pointer.
        dw      $ - gdt - 1 ; Limit.
        dq      gdt         ; Base.

err_msg:       db      "PANIC: No X64 Support!"
err_msg_len    equ     $ - err_msg

multiboot_magic         dd      0
multiboot_info          dd      0

[section .bss]
[BITS 32]
kernel_stack:   resb 0x1000
kernel_stack_top:
~~~

这就是boot.asm的全部，代码可能有点长，但是逻辑应该是很清晰的。首先判断是否支持64位，如果支持，那么加载GDT、开启PAE分页，跳转到64位模式开始执行。

需要指明的是，这段代码开启了分页，但是仅仅映射了前2MB内存，并且将4张表放在了从4K开始的16KB空间内。

### 解析Multiboot info

GRUB在引导过程中获取了许多与机器相关的信息，并将其放在Multiboot Info Struct中，其中我所关心的是Memory Map，可以通过下面的代码获得：

~~~ {.c}
multiboot_memory_map_t *mmap = (multiboot_memory_map_t *)mbi->mmap_addr;
uint32_t mmap_len = mbi->mmap_length;

int i = 0;
multiboot_memory_map_t *entry = mmap;

raw_write("[SIZE]        [ADDR]              [LENGTH]            [TYPE]", 80);
while (entry + sizeof(multiboot_memory_map_t) < mmap + mmap_len) {
    raw_write(int32_to_str(entry->size, 10), 160 + 80*i);
    raw_write(int64_to_str(entry->addr, 16), 174 + 80*i);
    raw_write(int64_to_str(entry->len,  16), 194 + 80*i);
    raw_write(int32_to_str(entry->type, 16), 214 + 80*i);
    ++i;
    entry = (multiboot_memory_map_t *)((unsigned int)entry + entry->size +sizeof(unsigned int));
}
~~~

其中的`raw_write`是调试用的输出函数，第二个参数是输出的位置。

### 中断

中断是一个绕不过的而且还非常重要的功能，要启用中断，首先要有一个IDT，64位环境下，IDT描述符的结构如下：

~~~ {.c}
struct idt_descriptor {
    uint16_t    offset_l;        // [0:15]
    uint16_t    selector;
    uint8_t     ist     :3,
                        :5;
    uint8_t     type    :4,
                s       :1,     // S: 0 -- system
                dpl     :2,     // DPL
                p       :1;     // Present
    uint16_t    offset_m;       // [16:31]
    uint32_t    offset_h;       // [32:63]
    uint32_t            :32;
} __attribute__((packed));
typedef struct idt_descriptor idt_descriptor_t;
~~~

作为一个最简单的例子，可以创建一个128个表项的IDT，每个条目的offset都指向同一个函数，然后调用int指令：

~~~ {.c}
idt_descriptor_t idt[128];

void make_idt_descriptor(idt_descriptor_t *desc, uint64_t offset, uint8_t type, uint8_t dpl) {
    desc->selector = 8;
    desc->ist = 0;
    desc->type = type;
    desc->s = 0;
    desc->dpl = dpl;
    desc->p = 1;
    desc->offset_l = (uint16_t) (offset & 0xffff);
    desc->offset_m = (uint16_t) ((offset >> 16) & 0xffff);
    desc->offset_h = (uint32_t) ((offset >> 32) & 0xffffffff);
}

static inline void lidt(void* base, uint16_t size) {
    struct {
        uint16_t length;
        uint64_t base;
    } __attribute__((packed)) idtr;
    idtr.length = size;
    idtr.base = (uint64_t)base;
    __asm__("lidt (%0)" :: "r"(&idtr));
}

void handler() {
    raw_write("INTERRUPTION!!!", 1600);
    while (1) {}
}

void setup_idt() {
    for (int i = 0; i < 256; ++i) {
        make_idt_descriptor(&idt[i], handler, 0xe, 0);
    }
    lidt(idt, 128*16-1);
    __asm__ ("int $0x20");
}
~~~

上面的代码运行之后会在屏幕上看到`INTERRUPTION!!!`的字样。

